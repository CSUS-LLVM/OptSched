set(OPTSCHEDSUPER_FLANG_INSTALL_PREFIX ${CMAKE_CURRENT_BINARY_DIR}/flang-install CACHE PATH "The directory to install flang")

set(OPTSCHEDSUPER_FLANG_BUILD_TYPE           Release                           CACHE STRING "The build type for flang")
set(OPTSCHEDSUPER_FLANG_LLVM_BUILD_TYPE      ${OPTSCHEDSUPER_FLANG_BUILD_TYPE} CACHE STRING "The build type for flang-llvm")
set(OPTSCHEDSUPER_FLANG_DRIVER_BUILD_TYPE    ${OPTSCHEDSUPER_FLANG_BUILD_TYPE} CACHE STRING "The build type for flang-driver")
set(OPTSCHEDSUPER_FLANG_OPENMP_BUILD_TYPE    ${OPTSCHEDSUPER_FLANG_BUILD_TYPE} CACHE STRING "The build type for openmp")
set(OPTSCHEDSUPER_FLANG_LIBPGMATH_BUILD_TYPE ${OPTSCHEDSUPER_FLANG_BUILD_TYPE} CACHE STRING "The build type for libpgmath")
set(OPTSCHEDSUPER_FLANG_COMPILER_BUILD_TYPE  ${OPTSCHEDSUPER_FLANG_BUILD_TYPE} CACHE STRING "The build type for the flang compiler")

set(OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR           ${CMAKE_GENERATOR}                     CACHE STRING "The generator to use to build flang")
set(OPTSCHEDSUPER_FLANG_LLVM_CMAKE_GENERATOR      ${OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build flang-llvm")
set(OPTSCHEDSUPER_FLANG_DRIVER_CMAKE_GENERATOR    ${OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build flang-driver")
set(OPTSCHEDSUPER_FLANG_OPENMP_CMAKE_GENERATOR    ${OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build openmp")
set(OPTSCHEDSUPER_FLANG_LIBPGMATH_CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build libpgmath")
set(OPTSCHEDSUPER_FLANG_COMPILER_CMAKE_GENERATOR  ${OPTSCHEDSUPER_FLANG_CMAKE_GENERATOR} CACHE STRING "The generator to use to build the flang compiler")

set(OPTSCHEDSUPER_FLANG_LLVM_GIT_REPO     https://github.com/CSUS-LLVM/llvm.git              CACHE STRING "The git repository to clone for flang llvm")
set(OPTSCHEDSUPER_FLANG_LLVM_GIT_TAG      optsched                                           CACHE STRING "The tag to checkout within the flang llvm git repository")
set(OPTSCHEDSUPER_FLANG_DRIVER_GIT_REPO   https://github.com/flang-compiler/flang-driver.git CACHE STRING "The git repository to clone for flang-driver")
set(OPTSCHEDSUPER_FLANG_DRIVER_GIT_TAG    release_60                                         CACHE STRING "The tag to checkout within the flang-driver git repository")
set(OPTSCHEDSUPER_FLANG_OPENMP_GIT_REPO   https://github.com/llvm-mirror/openmp.git          CACHE STRING "The git repository to clone for flang's openmp")
set(OPTSCHEDSUPER_FLANG_OPENMP_GIT_TAG    master                                             CACHE STRING "The tag to checkout within the flang openmp git repository")
set(OPTSCHEDSUPER_FLANG_COMPILER_GIT_REPO https://github.com/flang-compiler/flang.git        CACHE STRING "The git repository to clone for the flang compiler (including libpgmath)")
set(OPTSCHEDSUPER_FLANG_COMPILER_GIT_TAG  master                                             CACHE STRING "What to git checkout for flang (including libpgmath)")

set(OPTSCHEDSUPER_FLANG_LLVM_EXTRA_CMAKE_ARGS      "" CACHE STRING ";-separated list of extra arguments to pass to configure cmake")
set(OPTSCHEDSUPER_FLANG_DRIVER_EXTRA_CMAKE_ARGS    "" CACHE STRING ";-separated list of extra arguments to pass to configure cmake")
set(OPTSCHEDSUPER_FLANG_OPENMP_EXTRA_CMAKE_ARGS    "" CACHE STRING ";-separated list of extra arguments to pass to configure cmake")
set(OPTSCHEDSUPER_FLANG_LIBPGMATH_EXTRA_CMAKE_ARGS "" CACHE STRING ";-separated list of extra arguments to pass to configure cmake")
set(OPTSCHEDSUPER_FLANG_COMPILER_EXTRA_CMAKE_ARGS  "" CACHE STRING ";-separated list of extra arguments to pass to configure cmake")

function(setup_flang_external_projects cache_default_args)
  ExternalProject_Add(flang-llvm
    GIT_REPOSITORY ${OPTSCHEDSUPER_FLANG_LLVM_GIT_REPO}
    GIT_TAG ${OPTSCHEDSUPER_FLANG_LLVM_GIT_TAG}
    INSTALL_DIR ${OPTSCHEDSUPER_FLANG_INSTALL_PREFIX}
    CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_LLVM_CMAKE_GENERATOR}
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
      -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
      -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
      -DCMAKE_BUILD_TYPE=${OPTSCHEDSUPER_FLANG_LLVM_BUILD_TYPE}
      -DLLVM_TARGETS_TO_BUILD=X86
      -DLLVM_BUILD_TOOLS=ON
      -DLLVM_INCLUDE_TESTS=ON
      -DLLVM_OPTIMIZED_TABLEGEN=ON
      ${OPTSCHEDSUPER_FLANG_LLVM_EXTRA_CMAKE_ARGS}
    CMAKE_CACHE_DEFAULT_ARGS
      ${cache_default_args}
    USES_TERMINAL_CONFIGURE 1
    USES_TERMINAL_BUILD 1
    USES_TERMINAL_INSTALL 1
  )

  ExternalProject_Add(flang-driver
    GIT_REPOSITORY ${OPTSCHEDSUPER_FLANG_DRIVER_GIT_REPO}
    GIT_TAG ${OPTSCHEDSUPER_FLANG_DRIVER_GIT_TAG}
    DEPENDS flang-llvm
    INSTALL_DIR ${OPTSCHEDSUPER_FLANG_INSTALL_PREFIX}
    CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_DRIVER_CMAKE_GENERATOR}
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
      -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
      -DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}
      -DCMAKE_BUILD_TYPE=${OPTSCHEDSUPER_FLANG_DRIVER_BUILD_TYPE}
      -DLLVM_CONFIG=<INSTALL_DIR>/bin/llvm-config
      -DCLANG_ENABLE_STATIC_ANALYZER=ON
      ${OPTSCHEDSUPER_FLANG_DRIVER_EXTRA_CMAKE_ARGS}
    CMAKE_CACHE_DEFAULT_ARGS
      ${cache_default_args}
    USES_TERMINAL_CONFIGURE 1
    USES_TERMINAL_BUILD 1
    USES_TERMINAL_INSTALL 1
  )

  ExternalProject_Add(openmp
    GIT_REPOSITORY ${OPTSCHEDSUPER_FLANG_OPENMP_GIT_REPO}
    GIT_TAG ${OPTSCHEDSUPER_FLANG_OPENMP_GIT_TAG}
    DEPENDS flang-driver
    INSTALL_DIR ${OPTSCHEDSUPER_FLANG_INSTALL_PREFIX}
    CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_OPENMP_CMAKE_GENERATOR}
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
      -DCMAKE_BUILD_TYPE=${OPTSCHEDSUPER_FLANG_OPENMP_BUILD_TYPE}
      -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
      -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
      ${OPTSCHEDSUPER_FLANG_OPENMP_EXTRA_CMAKE_ARGS}
    CMAKE_CACHE_DEFAULT_ARGS
      ${cache_default_args}
    USES_TERMINAL_CONFIGURE 1
    USES_TERMINAL_BUILD 1
    USES_TERMINAL_INSTALL 1
  )

  ExternalProject_Add(libpgmath
    GIT_REPOSITORY ${OPTSCHEDSUPER_FLANG_COMPILER_GIT_REPO}
    GIT_TAG ${OPTSCHEDSUPER_FLANG_COMPILER_GIT_TAG}
    DEPENDS openmp
    SOURCE_SUBDIR runtime/libpgmath
    INSTALL_DIR ${OPTSCHEDSUPER_FLANG_INSTALL_PREFIX}
    CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_LIBPGMATH_CMAKE_GENERATOR}
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
      -DCMAKE_BUILD_TYPE=${OPTSCHEDSUPER_FLANG_LIBPGMATH_BUILD_TYPE}
      -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
      -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
      -DCMAKE_Fortran_COMPILER=<INSTALL_DIR>/bin/flang
      ${OPTSCHEDSUPER_FLANG_LIBPGMATH_EXTRA_CMAKE_ARGS}
    CMAKE_CACHE_DEFAULT_ARGS
      ${cache_default_args}
    USES_TERMINAL_CONFIGURE 1
    USES_TERMINAL_BUILD 1
    USES_TERMINAL_INSTALL 1
  )

  ExternalProject_Get_Property(libpgmath SOURCE_DIR)
  set(LIBPGMATH_SOURCE_DIR ${SOURCE_DIR})

  ExternalProject_Add(flang
    DEPENDS libpgmath
    SOURCE_DIR ${LIBPGMATH_SOURCE_DIR}
    DOWNLOAD_COMMAND ""
    INSTALL_DIR ${OPTSCHEDSUPER_FLANG_INSTALL_PREFIX}
    CMAKE_GENERATOR ${OPTSCHEDSUPER_FLANG_COMPILER_CMAKE_GENERATOR}
    CMAKE_ARGS
      -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
      -DCMAKE_BUILD_TYPE=${OPTSCHEDSUPER_FLANG_COMPILER_BUILD_TYPE}
      -DCMAKE_CXX_COMPILER=<INSTALL_DIR>/bin/clang++
      -DCMAKE_C_COMPILER=<INSTALL_DIR>/bin/clang
      -DCMAKE_Fortran_COMPILER=<INSTALL_DIR>/bin/flang
      -DLLVM_CONFIG=<INSTALL_DIR>/bin/llvm-config
      ${OPTSCHEDSUPER_FLANG_COMPILER_EXTRA_CMAKE_ARGS}
    CMAKE_CACHE_DEFAULT_ARGS
      ${cache_default_args}
    USES_TERMINAL_CONFIGURE 1
    USES_TERMINAL_BUILD 1
    USES_TERMINAL_INSTALL 1
  )
endfunction()
